/* ReSeed GAL Equations
 * Copyright 2022 SukkoPera <software@sukkology.net>
 * Based on work by Solder/Synergy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

/* These equations can be turned into a JED file through the WinCUPL software.
 * Too bad it's Windows-only and full of bugs...
 *
 * WARNING: This file will only work with V2 of the ReSeed board!
 */

Name ReSeed;
Partno TBD;
Date 30/08/2022;
Revision 02;
Designer SukkoPera + Solder/Synergy;
Company OpenRetroWorks;
Assembly None;
Location Italy;
Device g16v8as;		// Simple Mode

/** Inputs **/
Pin 1 = a15;
Pin 2 = a13;
Pin 3 = a11;
Pin 4 = a10;
Pin 5 = a9;
Pin 6 = a8;
Pin 7 = a7;
Pin 8 = a6;
Pin 9 = a4;
Pin 12 = a5;
Pin 13 = phi2;
Pin 17 = reset_active_lo;
Pin 18 = a12;
Pin 19 = a14;

/** Outputs **/
Pin 15 = sid;				// SID Enable (Active-low)
Pin 14 = port;				// Joystick Port Enable (Active-low)
Pin 16 = reset_active_hi;

/** Unused Pins **/
// Pin 11 =

/** Declarations and Intermediate Variable Definitions **/
Field addr = [a15..4];

/** Logic Equations **/

/* SID Chip Select */
de_range = addr:[FD40..FD5F];
hu_range = addr:[FE80..FE9F];
sid = !(phi2 & (de_range # hu_range));		// Addresses are valid on Phi2 rising edge

/* Joystick port Chip Select (Always FD80-FD8F) */
joy_range = addr:[FD80..FD8F];
port = !(phi2 & joy_range);

/* Active-low Reset to Active-high Reset (for 4520) */
reset_active_hi = !reset_active_lo;
